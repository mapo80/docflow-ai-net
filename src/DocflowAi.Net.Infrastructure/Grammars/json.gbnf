root        ::= wsp* (object / array) wsp*
object      ::= "{" wsp* (member (wsp* "," wsp* member)*)? wsp* "}"
member      ::= string wsp* ":" wsp* value
array       ::= "[" wsp* (value (wsp* "," wsp* value)*)? wsp* "]"
value       ::= string / number / object / array / true / false / null
string      ::= "\"" char* "\""
char        ::= [^"\\] / escape
escape      ::= "\\" ( ["\\/bfnrt] / unicode )
unicode     ::= "u" HEX HEX HEX HEX
number      ::= int frac? exp?
int         ::= "-"? ("0" / [1-9][0-9]*)
frac        ::= "." [0-9]+
exp         ::= [eE] [+\-]? [0-9]+
true        ::= "true"
false       ::= "false"
null        ::= "null"
wsp         ::= [ \t\n\r]
HEX         ::= [0-9a-fA-F]
