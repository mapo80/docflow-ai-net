root ::= ws json ws ;

json ::= object | array ;
object ::= '{' ws ( member ( ws ',' ws member )* )? ws '}' ;
member ::= string ws ':' ws value ;
array ::= '[' ws ( value ( ws ',' ws value )* )? ws ']' ;
value ::= string | number | object | array | 'true' | 'false' | 'null' ;

string ::= '"' char* '"' ;
char ::= [^"\\] | '\\' ( '"' | '\\' | '/' | 'b' | 'f' | 'n' | 'r' | 't' | 'u' hex hex hex hex ) ;
hex ::= [0-9a-fA-F] ;

number ::= '-'? ( '0' | [1-9] [0-9]* ) ( '.' [0-9]+ )? ( [eE] [+-]? [0-9]+ )? ;

ws ::= ( ' ' | '\t' | '\r' | '\n' )* ;
